100 DIM S(38),R(38),N(38)
110 N1=38:N2=12:N3=118
120 RESTORE 2530
130 FOR X=1 TO N1
140 READ S(X),L:R(X)=S(X):N(X)=S(X)+L-1
150 NEXT X
160 PRINT "HI, IM A ELIZA TELL ME YOUR PROBLEM"
170 LINE INPUT I$                         ' EINGABE EINER ZEILE VOM BENUTZER
175 IF I$="ENDE" THEN RUN "BASMENUE"
180 I$=" "+I$+" "
190 ' - - - - - B E S E I T I G E N   V O N   A P O S T R O P H E N - - - - - 
200 FOR L=1 TO LEN(I$)
210 IF MID$(I$,L,1)="'" THEN I$=LEFT$(I$,L-1)+RIGHT$(I$,LEN(I$)-L):GOTO 210
220 NEXT L
230 IF I$=P$ THEN PRINT "PLEASE DONT REPEAT YOURSELF":GOTO 170
270 ' - - - - S U C H E N   D E S   E R K E N N U N G S W O R T E S - - - -
280 RESTORE
290 S=0
300 FOR K=1 TO N1
305 IF S>0 THEN 340
310 READ K$
320 A=INSTR(I$,K$)
330 IF A<>0 THEN S=K:T=A:F$=K$
340 NEXT K
365 IF S>0 THEN K=S:L=T:GOTO 400
370 K=N1:GOTO 570
390 '- - - - - - - - -  K O N J U G A T I O N S   P H A S E - - - - - - - - - 
400 RESTORE 1200
410 C$=" "+RIGHT$(I$,LEN(I$)-LEN(F$)-L+1)
420 FOR X=1 TO N2/2
430 READ S$,R$
440 FOR L=1 TO LEN(C$)
450 IF L+LEN(S$)>LEN(C$) THEN 510
480 IF MID$(C$,L,LEN(S$))<>S$ THEN 510
490 C$=LEFT$(C$,L-1)+R$+RIGHT$(C$,LEN(C$)-L-LEN(S$)+1)
495 L=L+LEN(R$)
500 GOTO 540
510 IF L+LEN(R$)>LEN(C$) THEN 540
520 IF MID$(C$,L,LEN(R$))<>R$ THEN 540
530 C$=LEFT$(C$,L-1)+S$+RIGHT$(C$,LEN(C$)-L-LEN(R$)+1)
540 NEXT L
550 NEXT X
555 IF MID$(C$,2,1)=" " THEN C$=RIGHT$(C$,LEN(C$)-1) ' EIN SPACE IN ANTWORT
560 '- - - - - - - - A N T W O R T S A T Z   F I N D E N - - - - - - - - - - - 
570 RESTORE 1300
580 FOR X=1 TO R(K):READ F$:NEXT X      ' POSITIONIEREN
590 R(K)=R(K)+1:IF R(K)>N(K) THEN R(K)=S(K)
600 IF INSTR(F$,"*")=0 THEN PRINT F$:P$=I$:GOTO 170
610 A=INSTR(F$,"*")
620 PRINT LEFT$(F$,A-1);C$;RIGHT$(F$,LEN(F$)-A)
630 GOTO 170
1000 '- - - - - - - - - - - - - K E Y W O R D S - - - - - - - - - - - - - - - -
1050 DATA "CAN YOU","CAN I","YOU ARE","YOURE","I DONT","I FEEL"
1060 DATA "WHY DONT YOU","WHY CANT I","ARE YOU","I CANT","I AM","IM "
1070 DATA "YOU ","I WANT","WHAT","HOW","WHO","WHERE","WHEN","WHY"
1080 DATA "NAME","CAUSE","SORRY","DREAM","HELLO","HI ","MAYBE"
1090 DATA " NO ","YOUR","ALWAYS","THINK","ALIKE","YES","FRIEND"
1100 DATA "COMPUTER","ELIZA","MONEY","NOKEYFOUND"
1200 ' - - - - - - - - - K O N J U G A T I O N E N - - - - - - - - - - - - - - 
1230 DATA " ARE "," AM ","WERE ","WAS "," YOU "," I ","YOUR ","MY "
1235 DATA " IVE "," YOUVE "," IM "," YOURE "
1300 '- - - - - - - - - - - - A N T W O R T E N - - - - - - - - - - - - - - - -
1330 DATA "DON'T YOU BELIEVE THAT I CAN*"
1340 DATA "PERHAPS YOU WOULD LIKE TO BE ABLE TO*"
1350 DATA "YOU WANT ME TO BE ABLE TO*"
1360 DATA "PERHAPS YOU DON'T WANT TO*"
1365 DATA "DO YOU WANT TO BE ABLE TO*"
1370 DATA "WHAT MAKES YOU THINK I AM*"
1380 DATA "DOES IT PLEASE YOU TO BELIEVE I AM*"
1390 DATA "PERHAPS YOU WOULD LIKE TO BE*"
1400 DATA "DO YOU SOMETIMES WISH YOU WERE*"
1410 DATA "DON'T YOU REALLY*"
1420 DATA "WHY DON'T YOU*"
1430 DATA "DO YOU WISH TO BE ABLE TO*"
1440 DATA "DOES THAT TROUBLE YOU ?"
1450 DATA "TELL ME MORE ABOUT SUCH FEELINGS."
1460 DATA "DO YOU OFTEN FEEL*"
1470 DATA "DO YOU ENJOY FEELING*"
1480 DATA "DO YOU REALLY BELIEVE I DON'T*"
1490 DATA "PERHAPS IN GOOD TIME I WILL*"
1500 DATA "DO YOU WANT ME TO*" 
1510 DATA "DO YOU THINK YOU SHOULD BE ABLE TO *"
1520 DATA "WHY CAN'T YOU*"
1530 DATA "WHY ARE YOU INTERESTED IN WHETHER OR NOT I AM*"
1540 DATA "WOULD YOU PREFER IF I WHERE NOT*"
1550 DATA "PERHAPS IN YOUR FANTASIES I AM*"
1560 DATA "HOW DO YOU KNOW YOU CAN'T*"
1570 DATA "HAVE YOU TRIED ?"
1580 DATA "PERHAPS YOU CAN NOW*"
1590 DATA "DID YOU COME TO ME BECAUSE YOU ARE*"
1600 DATA "HOW LONG HAVE YOU BEEN*"
1610 DATA "DO YOU BELIEVE IT IS NORMAL TO BE*"
1620 DATA "DO YOU ENJOY BEING*"
1630 DATA "WE WERE DISCUSSING YOU - NOT ME."
1640 DATA "OH, I*"
1650 DATA "YOU'RE NOT REALLY TALKING ABOUT ME, ARE YOU ?" 
1660 DATA "WHAT WOULD IT MEAN TO YOU IF YOU GOT*"
1670 DATA "WHY DO YOU WANT*"
1680 DATA "SUPPOSE YOU SOON GOT*"
1690 DATA "WHAT IF YOU NEVER GOT*"
1700 DATA "I SOMETIMES ALSO WANT*" 
1710 DATA "WHY DO YOU ASK ?"
1720 DATA "DOES THAT QUESTION INTEREST YOU ?"
1730 DATA "WHAT ANSWER WOULD PLEASE YOU THE MOST ?"
1740 DATA "WHAT DO YOU THINK ?"
1750 DATA "ARE SUCH QUESTIONS IN YOURE MIND OFTEN ?"
1760 DATA "WHAT IS THAT YOU REALLY WANT TO KNOW ?"
1770 DATA "HAVE YOU ASKED ANYONE ELSE ?"
1780 DATA "HAVE YOU ASKED SUCH QUESTIONS BEFORE ?"
1790 DATA "WHAT ELSE COMES TO MIND WHEN YOU ASK THAT ?" 
1800 DATA "NAMES DON'T INTEREST ME."
1810 DATA "I DON'T CARE ABOUT NAMES - PLEASE GO ON."
1820 DATA "IS THAT THE REAL REASON ?"
1830 DATA "DON'T ANY OTHER REASONS COME TO MIND ?"
1840 DATA "DOES THAT REASON EXPLAIN ANYTHING ELSE ?"
1850 DATA "WHAT OTHER REASONS MIGHT THERE BE ?"
1860 DATA "PLEASE DON'T APOLOGIZE !"
1870 DATA "APOLOGIES ARE NOT NECESSARY."
1880 DATA "WHAT FEELINGS DO YOU HAVE WHEN YOU APOLOGIZE ?"
1890 DATA "DON'T BE SO DEFENSIVE!"
1900 DATA "WHAT DOES THAT DREAM SUGGEST TO YOU ?"
1910 DATA "DO YOU DREAM OFTEN ?"
1920 DATA "WHAT PERSONS APPEAR IN YOUR DREAMS ?"
1930 DATA "ARE YOU DISTURBED BY YOUR DREAMS ?"
1940 DATA "HOW DO YOU DO ... PLEASE STATE YOUR PROBLEM."
1950 DATA "YOU DON'T SEEM QUITE CERTAIN."
1960 DATA "WHY THE UNCERTAIN TONE ?"
1970 DATA "CAN'T YOU BE MORE POSITIVE ?" 
1980 DATA "YOU AREN'T SURE ?"
1990 DATA "DON'T YOU KNOW ?"
2000 DATA "WHY NO*"
2010 DATA "DONT SAY NO ITS ALWAYS SO NEGATIVE"
2020 DATA "WHY NOT ?"
2030 DATA "ARE YOU SURE ?"
2040 DATA "WHY NO ?"
2050 DATA "WHY ARE YOU CONCERNED ABOUT MY*"
2060 DATA "WHAT ABOUT YOUR OWN*"
2070 DATA "CAN'T YOU THINK OF A SPECIFIC EXAMPLE ?"
2080 DATA "WHEN ?"
2090 DATA "WHAT ARE YOU THINKING OF ?"
2100 DATA "REALLY, ALWAYS ?"
2110 DATA "DO YOU REALLY THINK SO ?"
2120 DATA "BUT YOU ARE NOT SURE YOU*"
2130 DATA "DO YOU DOUBT YOU*"
2140 DATA "IN WHAT WAY ?"
2150 DATA "WHAT RESEMBLANCE DO YOU SEE ?"
2160 DATA "WHAT DOES THE SIMILARITY SUGGEST TO YOU ?"
2170 DATA "WHAT OTHER CONNECTIONS DO YOU SEE ?"
2180 DATA "COULD THERE REALLY BE SOME CONNECTIONS ?"
2190 DATA "HOW ?"
2200 DATA "YOU SEEM QUITE POSITIVE."
2210 DATA "ARE YOU SURE ?"
2220 DATA "I SEE."
2230 DATA "I UNDERSTAND."
2240 DATA "WHY DO YOU BRING UP THE TOPIC OF FRIENDS ?"
2250 DATA "DO YOUR FRIENDS WORRY YOU ?"
2260 DATA "DO YOUR FRIENDS PICK ON YOU ?"
2270 DATA "ARE YOU SURE YOU HAVE ANY FRIENDS ?"
2280 DATA "DO YOU IMPOSE ON YOUR FRIENDS ?"
2290 DATA "PERHAPS YOUR LOVE FOR FRIENDS WORRIES YOU ?"
2300 DATA "DO COMPUTERS WORRY YOU ?"
2310 DATA "ARE YOU TALKING ABOUT ME IN PARTICULAR ?"
2320 DATA "ARE YOU FRIGHTENED BY MACHINES ?"
2330 DATA "WHY DO YOU MENTION COMPUTERS ?"
2340 DATA "WHAT DO YOU THINK MACHINES HAVE TO DO WITH YOUR PROBLEM ?"
2350 DATA "DON'T YOU THINK COMPUTERS CAN HELP PEOPLE ?"
2360 DATA "WHAT IS IT ABOUT MACHINES THAT WORRIES YOU ?"
2370 DATA "SAY, DO YOU HAVE ANY PSYCHOLOGICAL PROBLEMS ?"
2380 DATA "WHAT DOES THAT SUGGEST TO YOU ?"
2390 DATA "I SEE"
2400 DATA "I'M NOT SURE I UNDERSTAND YOU FULLY"
2410 DATA "COME COME ELUCIDATE YOUR THOUGHTS"
2420 DATA "CAN YOU ELABORATE ON THAT ?"
2430 DATA "THAT IS QUITE INTERESTING."
2440 DATA "WHY DO YOU HAVE PROBLEMS WITH MONEY ?"
2450 DATA "DO YOU THINK MONEY IS EVERYTHING ?"
2460 DATA "ARE YOU SURE THAT MONEY IS THE PROBLEM ?"
2470 DATA "I THINK WE WANT TO TALK ABOUT YOU, NOT ABOUT ME"
2480 DATA "WHATS ABOUT ME ?"
2490 DATA "WHY DO YOU ALWAYS BRING UP MY NAME ?"
2530 DATA 1,3,4,2,6,4,6,4,10,4,14,3,17,3,20,2,22,3,25,3
2540 DATA 28,4,28,4,32,3,35,5,40,9,40,9,40,9,40,9,40,9,40,9
2550 DATA 49,2,51,4,55,4,59,4,63,1,63,1,64,5,69,5,74,2,76,4
2560 DATA 80,3,83,7,90,3,93,6,99,7,116,3,113,3,106,7

